---
title: Soldity Functions
author: shivam
moduleNumber: module-1
modulePart: module-1-b
thisFileName: functions
orderNumber: "6"
modulePartDescription: In this part of the module, you will delve into the basics of the Solidity programming language, which is used for building Solana programs. You will take a deep dive into foundational Solidity for solana concepts.
---

## Objective

************************In this lession, you will:************************

- Understand function declaration in and out of a contract
- Know about the types of visibilities of functions.
- Understand state mutability and overloading.

## tl;dr

Solidity allows functions in and outside contracts, each with distinct visibility (public, private, internal, external). Functions can use named or positional arguments and have named return values. Functions can be view, pure, payable, or non-payable, with constructors in Ethereum being marked as payable to accept Ether. Function overloading is permitted based on differing argument types, and mangled names in the ABI distinguish overloaded functions. This flexibility makes Solidity suitable for smart contract development.

## Overview

A function can be declared inside a contract, where it has access to the contract storage variables, other contract functions etc. Functions can be also be declared outside a contract.

```soldity
// get_initial_bound is called from the constructor
function get_initial_bound() returns (uint256 value) {
    value = 102;
}

contract foo {
    uint256 bound = get_initial_bound();

    /** set bound for get with bound */
    function set_bound(uint256 _bound) public {
        bound = _bound;
    }

    // Clamp a value within a bound.
    // The bound can be set with set_bound().
    function get_with_bound(uint256 value) public view returns (uint256) {
        if (value < bound) {
            return value;
        } else {
            return bound;
        }
    }
}
```

Function can have any number of arguments. Function arguments may have names; if they do not have names then they cannot be used in the function body, but they will be present in the public interface.

The return values can have names as illustrated in the function get_initial_bound(). When all the return values have names, the return statement is not necessary at the end of the function. Instead, the values of the named return variables at the end of the function are automatically returned. However, it is still possible to explicitly return some values using the return statement.

## Function Visibility

Solidity functions have a visibility specifier that restricts the scope in which they can be called. Functions can be declared public, private, internal or external with the following definitions:

- **Public** functions can be called inside and outside a contract (e.g. by an RPC). They are present in the contract’s ABI or IDL.
- **Private** functions can only be called inside the contract they are declared.
- **Internal** functions can only be called internally within the contract or by any contract inherited contract.
- **External** functions can exclusively be called by other contracts or directly by an RPC. They are also present in the contract’s ABI or IDL.

Both public and external functions can be called using the syntax `this.func()`. In this case, the arguments are ABI encoded for the call, as it is treated like an external call. This is the only way to call an external function from inside the same contract it is defined. This method, however, should be avoided for public functions, as it will be more costly to call them than simply using `func()`.

If a function is defined outside a contract, it cannot have a visibility specifier (e.g. `public`).

**_Arguments Passing and Return Values_**

Function arguments can be passed either by position or by name. When they are called by name, arguments can be in any order. However, functions with anonymous arguments (arguments without name) cannot be called this way.

```Solidity
contract foo {
    function bar(uint32 x, bool y) public returns (uint32) {
        if (y) {
            return 2;
        }
        return 3;
    }

    function test() public {
        uint32 a = bar(102, false);
        a = bar({y: true, x: 302});
    }
}
```

If the function has a single return value, this can be assigned to a variable. If the function has multiple return values, these can be assigned using the [Destructuring Statement](https://solang.readthedocs.io/en/v0.3.3/language/statements.html#destructuring) assignment statement:

```Soldity
contract foo {
    function bar1(uint32 x, bool y) public returns (address, bytes32) {
        return (address(3), hex"01020304");
    }

    function bar2(uint32 x, bool y) public returns (bool) {
        return !y;
    }

    function test() public {
        (address f1, bytes32 f2) = bar1(102, false);
        bool f3 = bar2({x: 255, y: true});
    }
}
```

It is also possible to call functions on other contracts, which is also known as calling external functions. The called function must be declared public. Calling external functions requires ABI encoding the arguments, and ABI decoding the return values. The syntax for calling a contract is the same as that of the external call, except that it must be done on a contract type variable.

```Solidity
contract foo {
    function bar1(uint32 x, bool y) public returns (address, bytes32) {
        return (address(3), hex"01020304");
    }

    function bar2(uint32 x, bool y) public returns (bool) {
        return !y;
    }
}

contract bar {
    function test(address f) public {
        (address f1, bytes32 f2) = foo.bar1{program_id: f, accounts: []}(102, false);
        bool f3 = foo.bar2{program_id: f, accounts: []}({x: 255, y: true});
    }
}
```

## State Mutability

Functions in a contract can either read or write data in the contract storage. Functions that do not write data, can be executed off-chain, which is faster and does not require write access or any balance. There are two types of functions that do not write data: view and pure. Pure functions do not read data from the storage, while view functions do.

On the other hand, functions that write data come in two types: payable and non-payable. Non-payable is the default type, and functions that are not meant to receive any value should not be marked as payable. The compiler will check that every call does not include any value, and there are also runtime checks that will revert the function if any value is sent.

Lastly, the constructor can be marked as payable, which means that a value can be passed along with the constructor.

## Function Overloading

Multiple functions with the same name can be declared, as long as the arguments are different in at least one of two ways:

- The number of arguments must be different
- The type of at least one of the arguments is different

A function cannot be overloaded by changing the return types or number of returned values.

Here is an example of an overloaded function:

```Solidity
contract shape {
    int64 bar;

    function abs(int256 val) public returns (int256) {
        if (val >= 0) {
            return val;
        } else {
            return -val;
        }
    }

    function abs(int64 val) public returns (int64) {
        if (val >= 0) {
            return val;
        } else {
            return -val;
        }
    }

    function foo(int64 x) public {
        bar = int64(abs(x));
    }
}
```

In the function foo, abs() is called with an `int64` so the second implementation of the function abs() is called. Solana runtime requires unique function names, so overloaded function names will be mangled in the ABI or the IDL.

The function name will be concatenated with all of its argument types, separated by underscores, using the following rules:

- Struct types are represented by their field types (preceded by an extra underscore).
- Enum types are represented as their underlying `uint8` type.
- Array types are recognizable by having `Array` appended.
- Fixed size arrays will additionally have their length appended as well.

The following example illustrates some overloaded functions and their mangled name:

```Solidity
enum E {
    v1,
    v2
}
struct S {
    int256 i;
    bool b;
    address a;
}

contract C {
    // foo_
    function foo() public pure {}

    // foo_uint256_addressArray2Array
    function foo(uint256 i, address[2][] memory a) public pure {}

    // foo_uint8Array2__int256_bool_address
    function foo(E[2] memory e, S memory s) public pure {}
}
```

Congratulations, you are now proficient with the basics of Solidity in Solang!
